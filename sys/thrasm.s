# thrasm.s - Special functions called from thread.c
#
# Copyright (c) 2024-2026 University of Illinois
# SPDX-License-identifier: NCSA
#

        .text
        .global switch_running_thread
        .type   switch_running_thread, @function

switch_running_thread:

# void switch_running_thread(struct thread * next_thread);
# 
# Switches from the currently running thread to another thread and returns when
# the current thread is scheduled to run again. This function is called by
# running_thread_yield() defined in thread.c. Argument /next_thread/ is the
# thread to be resumed. Instead of returning, jumps to finish_switch(), which is
# also defined in thread.c, which returns via /ra/ to running_thread_yield().
# 
# tp = pointer to struct thread of current thread (to be suspended)
# a0 = pointer to struct thread of thread to be resumed
# 
# The first element of struct thread is:
#
#     struct thread_context {
#         uint64_t s[12];
#         void (*ra)(uint64_t);
#         void * sp;
#     };
#

        sd      s0, 0*8(tp)
        sd      s1, 1*8(tp)
        sd      s2, 2*8(tp)
        sd      s3, 3*8(tp)
        sd      s4, 4*8(tp)
        sd      s5, 5*8(tp)
        sd      s6, 6*8(tp)
        sd      s7, 7*8(tp)
        sd      s8, 8*8(tp)
        sd      s9, 9*8(tp)
        sd      s10, 10*8(tp)
        sd      s11, 11*8(tp)
        sd      ra, 12*8(tp)
        sd      sp, 13*8(tp)

        mv      t0, tp
        mv      tp, a0
        mv      a0, t0

        ld      sp, 13*8(tp)
        ld      ra, 12*8(tp)
        ld      s11, 11*8(tp)
        ld      s10, 10*8(tp)
        ld      s9, 9*8(tp)
        ld      s8, 8*8(tp)
        ld      s7, 7*8(tp)
        ld      s6, 6*8(tp)
        ld      s5, 5*8(tp)
        ld      s4, 4*8(tp)
        ld      s3, 3*8(tp)
        ld      s2, 2*8(tp)
        ld      s1, 1*8(tp)
        ld      s0, 0*8(tp)
                
        j       finish_thread_switch


        .global start_thread
        .type   start_thread, @function

start_thread:

# Code fragment used to start a new thread. When a thread is spawned, it is
# placed on the ready list and the s-registers in its saved context are
# populated with the arguments to the thread function where execution of the new
# thread should start. This fragment populated the a0-a7 argument registers with
# the contents of s0-s7, and initializes the /fp/ and /ra/ registers from s10
# and s11. The address of the thread function is in s8.

        mv      a0, s0
        mv      a1, s1
        mv      a2, s2
        mv      a3, s3
        mv      a4, s4
        mv      a5, s5
        mv      a6, s6
        mv      a7, s7
        mv      fp, s10
        mv      ra, s11
        jr      s8

# Statically allocated stack for the idle thread.

        .section        .data.stack, "wa", @progbits
        .balign          16
        
        .equ            IDLE_STACK_SIZE, 4096

        .global         _idle_stack_lowest
        .type           _idle_stack_lowest, @object
        .size           _idle_stack_lowest, IDLE_STACK_SIZE

        .global         _idle_stack_anchor
        .type           _idle_stack_anchor, @object
        .size           _idle_stack_anchor, 2*8

_idle_stack_lowest:
        .fill   IDLE_STACK_SIZE, 1, 0xBB

_idle_stack_anchor:
        .dword  0 # ktp
        .dword  0 # kgp
        .end

